#*****************************************************************************
#* mpegdemux                                                                 *
#*****************************************************************************

#*****************************************************************************
#* File name:     configure.in                                               *
#* Created:       2003-02-01 by Hampa Hug <hampa@hampa.ch>                   *
#* Last modified: 2003-10-21 by Hampa Hug <hampa@hampa.ch>                   *
#* Copyright:     (C) 2003 by Hampa Hug <hampa@hampa.ch>                     *
#*****************************************************************************

#*****************************************************************************
#* This program is free software. You can redistribute it and / or modify it *
#* under the terms of the GNU General Public License version 2 as  published *
#* by the Free Software Foundation.                                          *
#*                                                                           *
#* This program is distributed in the hope  that  it  will  be  useful,  but *
#* WITHOUT  ANY   WARRANTY,   without   even   the   implied   warranty   of *
#* MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU  General *
#* Public License for more details.                                          *
#*****************************************************************************

# $Id: configure.in,v 1.13 2003/10/21 04:41:11 hampa Exp $

AC_PREREQ(2.50)

AC_INIT(Makefile.in)
AC_CONFIG_HEADER(src/config.h)


#-----------------------------------------------------------------------------
# package version

AC_MSG_CHECKING([mpegdemux version])
if test ! -r "$srcdir/version" ; then
	AC_MSG_RESULT([file 'version' not found in source directory])
	exit 1
fi
oldifs=$IFS
IFS="$IFS."
read p MPEGDEMUX_VERSION_MAJ MPEGDEMUX_VERSION_MIN MPEGDEMUX_VERSION_MIC dstr r < "$srcdir/version"
IFS=$oldifs
if test "x$dstr" != "x" ; then
	MPEGDEMUX_VERSION_REL=0
	if test "x$dstr" = "xcvs" ; then
		dstr=`date +%Y%m%d`
		MPEGDEMUX_VERSION_STR="cvs-$dstr"
	else
		MPEGDEMUX_VERSION_STR="$dstr"
	fi
else
	MPEGDEMUX_VERSION_REL=1
	MPEGDEMUX_VERSION_STR="$MPEGDEMUX_VERSION_MAJ.$MPEGDEMUX_VERSION_MIN.$MPEGDEMUX_VERSION_MIC"
fi
AC_SUBST(MPEGDEMUX_VERSION_MAJ)
AC_SUBST(MPEGDEMUX_VERSION_MIN)
AC_SUBST(MPEGDEMUX_VERSION_MIC)
AC_SUBST(MPEGDEMUX_VERSION_STR)
AC_SUBST(MPEGDEMUX_VERSION_REL)
AC_DEFINE_UNQUOTED(MPEGDEMUX_VERSION_MAJ, $MPEGDEMUX_VERSION_MAJ)
AC_DEFINE_UNQUOTED(MPEGDEMUX_VERSION_MIN, $MPEGDEMUX_VERSION_MIN)
AC_DEFINE_UNQUOTED(MPEGDEMUX_VERSION_MIC, $MPEGDEMUX_VERSION_MIC)
AC_DEFINE_UNQUOTED(MPEGDEMUX_VERSION_STR, "$MPEGDEMUX_VERSION_STR")
AC_DEFINE_UNQUOTED(MPEGDEMUX_VERSION_REL, $MPEGDEMUX_VERSION_REL)
AC_MSG_RESULT([$MPEGDEMUX_VERSION_STR])


dnl -----------------------------------------------------------------------

MPEGDEMUX_CFG_DATE=`date +%Y-%m-%d`
MPEGDEMUX_CFG_TIME=`date +%T`
AC_SUBST(MPEGDEMUX_CFG_DATE)
AC_SUBST(MPEGDEMUX_CFG_TIME)
AC_DEFINE_UNQUOTED(MPEGDEMUX_CFG_DATE, "$MPEGDEMUX_CFG_DATE")
AC_DEFINE_UNQUOTED(MPEGDEMUX_CFG_TIME, "$MPEGDEMUX_CFG_TIME")


dnl -----------------------------------------------------------------------
dnl Checks for programs.

AC_PROG_CC
AC_PROG_INSTALL
AC_PATH_PROG(AR, ar, ar)
AC_PROG_MAKE_SET
AC_PROG_LN_S


dnl --------------------------------------------------------------------------
dnl Checks for arguments

AC_ARG_ENABLE(largefile,
  AC_HELP_STRING([--disable-largefile], [Disable large file support]),
  opt_large=$enableval
)

if test "$opt_large" = "no" ; then
	MPEGDEMUX_LARGE_FILE="no"
else
	AC_DEFINE(MPEGDEMUX_LARGE_FILE)
	MPEGDEMUX_LARGE_FILE="yes"
fi
AC_SUBST(MPEGDEMUX_LARGE_FILE)


dnl -----------------------------------------------------------------------
dnl Checks for header files.

AC_HEADER_STDC


dnl -----------------------------------------------------------------------
dnl Checks for libraries


AC_CHECK_SIZEOF(long long, 8)
AC_CHECK_SIZEOF(unsigned long long, 8)


AC_OUTPUT(Makefile.inc Makefile src/Makefile)


dnl -----------------------------------------------------------------------

eval "mpegdemux_bindir=$bindir"
eval "mpegdemux_bindir=$mpegdemux_bindir"

eval "mpegdemux_mandir=$mandir"
eval "mpegdemux_mandir=$mpegdemux_mandir"

echo ""
echo "mpegdemux $MPEGDEMUX_VERSION_STR is now configured:"

echo "  CC:                    $CC"
echo "  CFLAGS:                \"$CFLAGS\""
echo "  LDFLAGS:               \"$LDFLAGS\""
echo ""
echo "  prefix:                $prefix"
echo "  bindir:                $mpegdemux_bindir"
echo "  mandir:                $mpegdemux_mandir"
