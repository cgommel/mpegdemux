#*****************************************************************************
#* mpegdemux                                                                 *
#*****************************************************************************

#*****************************************************************************
#* File name:   configure.in                                                 *
#* Created:     2003-02-01 by Hampa Hug <hampa@hampa.ch>                     *
#* Copyright:   (C) 2003-2009 Hampa Hug <hampa@hampa.ch>                     *
#*****************************************************************************

#*****************************************************************************
#* This program is free software. You can redistribute it and / or modify it *
#* under the terms of the GNU General Public License version 2 as  published *
#* by the Free Software Foundation.                                          *
#*                                                                           *
#* This program is distributed in the hope  that  it  will  be  useful,  but *
#* WITHOUT  ANY   WARRANTY,   without   even   the   implied   warranty   of *
#* MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU  General *
#* Public License for more details.                                          *
#*****************************************************************************


AC_PREREQ(2.50)

AC_INIT(mpegdemux)
AC_CONFIG_SRCDIR(Makefile.in)
AC_CONFIG_HEADER(src/config.h)


#-----------------------------------------------------------------------------
# package version

AC_MSG_CHECKING([package version])
if test ! -r "$srcdir/version" ; then
	AC_MSG_RESULT([file 'version' not found in source directory])
	exit 1
fi

read p MPEGDEMUX_VERSION_MAJ MPEGDEMUX_VERSION_MIN MPEGDEMUX_VERSION_MIC dstr r < "$srcdir/version"

MPEGDEMUX_VERSION_STR=$dstr

if test "x$dstr" = "xscm" ; then
	if test -d "$srcdir/.svn" ; then
		tmp=svninfo.$$.tmp
		svn info "$srcdir" > "$tmp" 2> /dev/null

		date=`date "+%Y-%m-%d"`
		rev=""

		while read a b c d e ; do
			case "$a$b$c$d" in
			"Revision:"*)
				rev="$b"
				;;
			"LastChangedDate:"*)
				date="$d"
				;;
			esac
		done < "$tmp"

		rm -f "$tmp"

		test -n "$rev" && rev="-r$rev"
		date=`echo "$date" | sed -e "s/-//g"`
		MPEGDEMUX_VERSION_STR="$date$rev"
	elif test -d "$srcdir/.git" ; then
		tmp=gitlog.$$.tmp
		( cd "$srcdir" && git log -1 --date=iso --pretty="format:%h %cd" HEAD ) > "$tmp" 2> /dev/null
		read hash date time rest < "$tmp"
		rm -f "$tmp"
		if test -n "$date" -a -n "$hash" ; then
			date=`echo "$date" | sed -e "s/-//g"`
			MPEGDEMUX_VERSION_STR="$date-$hash"
		fi
	fi
elif test "x$dstr" = "x" ; then
	MPEGDEMUX_VERSION_STR="$MPEGDEMUX_VERSION_MAJ.$MPEGDEMUX_VERSION_MIN.$MPEGDEMUX_VERSION_MIC"
fi
AC_SUBST(MPEGDEMUX_VERSION_MAJ)
AC_SUBST(MPEGDEMUX_VERSION_MIN)
AC_SUBST(MPEGDEMUX_VERSION_MIC)
AC_SUBST(MPEGDEMUX_VERSION_STR)
AC_DEFINE_UNQUOTED(MPEGDEMUX_VERSION_MAJ, $MPEGDEMUX_VERSION_MAJ)
AC_DEFINE_UNQUOTED(MPEGDEMUX_VERSION_MIN, $MPEGDEMUX_VERSION_MIN)
AC_DEFINE_UNQUOTED(MPEGDEMUX_VERSION_MIC, $MPEGDEMUX_VERSION_MIC)
AC_DEFINE_UNQUOTED(MPEGDEMUX_VERSION_STR, "$MPEGDEMUX_VERSION_STR")
AC_MSG_RESULT([$MPEGDEMUX_VERSION_STR ($MPEGDEMUX_VERSION_MAJ.$MPEGDEMUX_VERSION_MIN.$MPEGDEMUX_VERSION_MIC)])


#-----------------------------------------------------------------------------
# programs

AC_PROG_CC
AC_PROG_INSTALL
case "$INSTALL" in
.*)
	d=`dirname "$INSTALL"`
	f=`basename "$INSTALL"`
	INSTALL=`cd "$d" && pwd`/"$f"
	;;
esac
AC_PATH_PROG(AR, ar, ar)
AC_PROG_RANLIB
AC_PROG_MAKE_SET
AC_PROG_LN_S


#-----------------------------------------------------------------------------
# arguments

AC_ARG_ENABLE(largefile,
	AC_HELP_STRING([--disable-largefile], [Disable large file support]),
	opt_large=$enableval
)

if test "$opt_large" = "no" ; then
	MPEGDEMUX_LARGE_FILE=0
else
	AC_DEFINE(MPEGDEMUX_LARGE_FILE)
	MPEGDEMUX_LARGE_FILE=1
fi
AC_SUBST(MPEGDEMUX_LARGE_FILE)


#-----------------------------------------------------------------------------
# header files

AC_HEADER_STDC


#-----------------------------------------------------------------------------
# output

AC_OUTPUT(Makefile Makefile.inc)


#-----------------------------------------------------------------------------
# status

echo ""
echo "mpegdemux $MPEGDEMUX_VERSION_STR is now configured:"

echo "  CC:                    $CC $CFLAGS"
echo "  LD:                    $CC $LDFLAGS"
echo ""
echo "  prefix:                $prefix"
